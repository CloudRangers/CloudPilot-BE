plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.cloudrangers'
version = '0.0.1-SNAPSHOT'
description = 'cloudpilot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.mysql:mysql-connector-j'

	// RabbitMQ
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.retry:spring-retry'

	// JSON 타입 처리
	implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.7.0'

	// Jackson LocalDateTime 처리
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// RabbitMQ Test
	testImplementation 'org.springframework.amqp:spring-rabbit-test'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

def loadDotenv() {
	def f = file('.env'); if (!f.exists()) return [:]
	def m = [:]
	f.eachLine { raw ->
		def line = raw.trim()
		if (!line || line.startsWith('#')) return
		def i = line.indexOf('=')
		if (i <= 0) return
		m[line.substring(0,i).trim()] = line.substring(i+1).trim()
	}
	m
}

tasks.named('bootRun') {
	def envs = loadDotenv()
	if (!envs.isEmpty()) environment envs
	environment 'SPRING_PROFILES_ACTIVE',
			System.getenv('SPRING_PROFILES_ACTIVE') ?: envs['SPRING_PROFILES_ACTIVE'] ?: 'local'
}

tasks.withType(Test) {
	useJUnitPlatform()
	def envs = loadDotenv()
	if (!envs.isEmpty()) environment envs
	environment 'SPRING_PROFILES_ACTIVE',
			System.getenv('SPRING_PROFILES_ACTIVE') ?: envs['SPRING_PROFILES_ACTIVE'] ?: 'test'
}